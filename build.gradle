buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url 'https://jitpack.io/' }
    }
    dependencies {
        classpath 'com.github.Fox2Code.FoxLoader:dev:1.2.17'
        classpath 'com.github.johnrengelman:shadow:8.1.1'
        classpath 'io.freefair.gradle:lombok-plugin:8.3'
    }
}

apply plugin: 'java'
apply plugin: 'io.freefair.lombok'

version '0.0.3'

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'foxloader.dev'

configurations {
    clientImplementation {
        canBeResolved = true
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    clientImplementation 'com.github.pircbotx:pircbotx:2.3.1'
    clientImplementation 'commons-codec:commons-codec:1.15'
    clientImplementation 'org.slf4j:slf4j-api:2.0.9'
    clientImplementation 'org.slf4j:slf4j-jdk14:2.0.9'
    clientImplementation 'io.github.llamalad7:mixinextras-common:0.2.0-rc.5'

	annotationProcessor 'io.github.llamalad7:mixinextras-common:0.2.0-rc.5'
}

shadowJar {
    archiveClassifier.set("shadow")
    from sourceSets.client.output
    configurations = [project.configurations.clientImplementation]
    mergeServiceFiles()
    dependencies {
        include dependency('com.github.pircbotx:pircbotx:2.3.1')
        include dependency('commons-codec:commons-codec:1.15')
        include dependency('org.slf4j:slf4j-api:2.0.9')
        include dependency('org.slf4j:slf4j-jdk14:2.0.9')
        include dependency('io.github.llamalad7:mixinextras-common:0.2.0-rc.5')
    }
    relocate 'org.pircbotx', 'com.lightmanlp.ircsubchat.libs.pircbotx'
    relocate 'org.apache.commons.codec', 'com.lightmanlp.ircsubchat.libs.apache.commons.codec'
    relocate 'org.slf4j', 'com.lightmanlp.ircsubchat.libs.slf4j'
    // exploded
    relocate 'com.llamalad7.mixinextras', 'com.lightmanlp.ircsubchat.libs.mixinextras'
}

build.dependsOn shadowJar

foxloader {
    // forceReload = true
    modId = "ircsubchat"
    modName = "IRC Subchat"
    modVersion = project.version
    modDesc = "Ingame IRC subchat"
    clientMod = "com.lightmanlp.ircsubchat.IRCSubchatMod"
    // serverMod = "com.fox2code.examplemod.ExampleModServer"
    // Development tools don't like that both client and server define
    // the same classes, so uncomment to disable the side inclusion.
    includeClient = true
    includeServer = false
}

apply plugin: 'maven-publish'
publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.github.lightmanlp'
            artifactId = 'ircsubchat'
            version = project.version

            from components.java
        }
    }
}
